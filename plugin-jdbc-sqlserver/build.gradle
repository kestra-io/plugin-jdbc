project.description = 'Query SQL Server databases with Kestra\'s JDBC plugin.'

jar {
    manifest {
        attributes(
                "X-Kestra-Name": project.name,
                "X-Kestra-Title": "Microsoft SQL Server",
                "X-Kestra-Group": project.group + ".jdbc.sqlserver",
                "X-Kestra-Description": project.description,
                "X-Kestra-Version": project.version
        )
    }
}

dependencies {
    // Library that gives the ability to obtain tokens from Azure AD v2
    implementation("com.microsoft.azure:msal4j:1.23.0")
    implementation("com.microsoft.sqlserver:mssql-jdbc:13.3.1.jre11-preview")
    implementation project(':plugin-jdbc')

    // Connect using Microsoft Entra
    // https://learn.microsoft.com/en-us/sql/connect/jdbc/connecting-using-azure-active-directory-authentication?view=sql-server-ver16#connect-using-activedirectorydefault-authentication-mode
    api (group: 'com.azure', name: 'azure-identity') {
        // exclude libraries already provided by Kestra
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jsr310'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-guava'
    }

    testImplementation project(':plugin-jdbc').sourceSets.test.output
    testRuntimeOnly project(path: ':plugin-jdbc', configuration: 'shadow')
}
